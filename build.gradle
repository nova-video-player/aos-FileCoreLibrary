apply plugin: 'com.android.library'

repositories {
    google()
    mavenCentral()
    mavenLocal()
    maven { url 'https://jitpack.io' }
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile).configureEach {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
        // to generate the required jar dependencies e.g. cling seamless
        preBuild.dependsOn(ndkBuild)
    }
}

android {
    namespace 'com.archos.filecorelibrary'
    compileSdk 34
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            //tmp prebuilt jni
            jniLibs.srcDirs = ['libs']
            //disable automatic jni build rules
            jni.srcDirs = []
        }
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        multiDexEnabled true
    }

    tasks.withType(JavaCompile).configureEach {
        compileTask -> compileTask.dependsOn ndkBuild
    }
    clean.dependsOn 'ndkClean'

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        renderScript true
        aidl true
    }
}

tasks.register('ndkBuild', Exec) {
    workingDir = "../"

    outputs.upToDateWhen {
        file("libs/arm64-v8a/libc++_shared.so").exists()
        file("libs/armeabi-v7a/libc++_shared.so").exists()
        file("libs/x86/libc++_shared.so").exists()
        file("libs/x86_64/libc++_shared.so").exists()
        //file("libs/jcifs-ng.jar").exists()
    }
    //OutputFiles/OutputDirectories
    List<String> myCmd = new ArrayList<String>()
    myCmd.add("make")
    myCmd.add("native_libnativehelper")
    myCmd.add("native_build_FileCoreLibrary")
    //myCmd.add("jcifs-ng")
    commandLine myCmd
}

tasks.register('ndkClean', Exec) {
    workingDir = "../"
    List<String> myCmd = new ArrayList<String>()
    myCmd.add("make")
    myCmd.add("native_clean")
    //myCmd.add("external_clean_jcifs-ng")
    commandLine myCmd
}

// ensure there is no loggers other than logback
configurations.configureEach {
    exclude group: "org.slf4j", module: "slf4j-log4j12"
    exclude group: "log4j", module: "log4j"
}

dependencies {
    api 'eu.agno3.jcifs:jcifs-ng:2.1.10'
    api 'com.github.mwiede:jsch:0.2.16'
    // update to 3.9.0 (java 7->8) breaks ftp on firestick4k (not max) had to use 3.8.0
    // https://commons.apache.org/proper/commons-net/release-notes.html need to stick with java7: max 3.8.0
    // versions of commons-net otherwise we get java.time.Duration ClassNotFoundException even with threetenabp
    implementation 'commons-net:commons-net:3.8.0'
    implementation 'com.github.nova-video-player:sardine-android:0.0.2'
    implementation 'com.hierynomus:smbj:0.13.0'
    implementation 'com.hierynomus:sshj:0.38.0'
    implementation 'oro:oro:2.0.8'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'org.slf4j:slf4j-api:2.0.11'
    implementation 'com.github.tony19:logback-android:3.0.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'
    implementation 'io.sentry:sentry:7.0.0'
}

